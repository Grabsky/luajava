plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/lualink/fast-reflection")
        name = "FastReflection"
        credentials {
            username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }

}

group = rootProject.group
version = rootProject.version

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'com.badlogicgames.gdx:gdx-jnigen-loader:2.5.2'
    implementation 'me.sunlan:fast-reflection:1.0.3'
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

configurations {
    instrumentedJars {
        canBeConsumed = true
        canBeResolved = false
        extendsFrom api, implementation, runtimeOnly
    }
}

artifacts {
    instrumentedJars(jar)
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'party.iroiro.luajava')
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/LuaLink/luajava"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        register("maven", MavenPublication.class) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}
